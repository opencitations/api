#url /api/v2
#type api
#base https://w3id.org/oc/index/doci
#title REST API for DOCI, the OpenCitations Index of Datacite open DOI-to-DOI references
#description This document describe the REST API for accessing the data stored in [DOCI](https://w3id.org/oc/index/doci) hosted by [OpenCitations](http://opencitations.net). This API implements operations to retrieve the citation data for all the references to other works appearing in a particular bibliographic entity, or the citation data for all the references appearing in other works to a particular bibliographic entity, given the DOI of a bibliographic entity, or to retrieve citation data about a particular citation identified by means of its [Open Citation Identifier (OCI)](https://opencitations.wordpress.com/2018/03/12/citations-as-first-class-data-entities-open-citation-identifiers/).

All the present operations return either a JSON document (default) or a CSV document according to the mimetype specified in the `Accept` header of the request. If you would like to suggest an additional operation to be included in this API, please use the [issue tracker](https://github.com/opencitations/api/issues) of the OpenCitations APIs available on GitHub.

If you are going to use the REST APIs within an application/code, we encourage you to get the [OpenCitations Access Token](https://opencitations.net/accesstoken) and specify it in the "authorization" header of your REST API call. Here is a usage example in Python:

<pre style="height: 150px"><code>
from requests import get

API_CALL = "https://opencitations.net/index/doci/api/v1/references/10.1594/pangaea.108852"
HTTP_HEADERS = {"authorization": "YOUR-OPENCITATIONS-ACCESS-TOKEN"}

get(API_CALL, headers=HTTP_HEADERS)
</code></pre>

#version Version 2.0.0 (2022-12-20)
#contacts [contact@opencitations.net](mailto:contact@opencitations.net)
#license This document is licensed with a [Creative Commons Attribution 4.0 International License](https://creativecommons.org/licenses/by/4.0/legalcode), while the REST API itself has been created using [RAMOSE](https://github.com/opencitations/ramose), the *Restful API Manager Over SPARQL Endpoints* created by [Silvio Peroni](https://orcid.org/0000-0003-0530-4305), which is licensed with an [ISC license](https://opensource.org/licenses/ISC).
#endpoint http://127.0.0.1:3001/blazegraph/sparql
#method post
#addon indexapi_v2

#url /references/{doi}
#type operation
#doi str(10\..+)
#preprocess lower(doi) --> encode(doi)
#postprocess decode_doi(citing, cited)
#method get
#description This operation retrieves the citation data for all the outgoing references to other cited works appearing in the reference list of the bibliographic entity identified by the input DOI.

The fields returned by this operation are:

* *oci*: the Open Citation Identifier (OCI) of the citation in consideration;
* *citing*: the DOI of the citing entity;
* *cited*: the DOI of the cited entity;
* *creation*: the creation date of the citation according to the [ISO date format](https://en.wikipedia.org/wiki/ISO_8601) `YYYY-MM-DD`, which corresponds to the publication date of the citing entity;
* *timespan*: the interval between the publication date of the cited entity and the publication date of the citing entity, expressed using the [XSD duration format](https://www.w3.org/TR/xmlschema11-2/#duration) `PnYnMnD`;
* *journal_sc*: it records whether the citation is a journal self-citations (i.e. the citing and the cited entities are published in the same journal);
* *author_sc*: it records whether the citation is an author self-citation (i.e. the citing and the cited entities have at least one author in common).
#call /references/10.1594/pangaea.108852
#output_json [
    {
      "id": "oci:08001050904362510231610141037010008080502-08004030109362124370109090737040237013700000001",
      "citing": "doi:10.1594/pangaea.108852",
      "cited": "doi:10.4319/lo.1997.42.1.0001",
      "creation": "2003",
      "timespan": "",
      "journal_sc": "no",
      "author_sc": "no"
    }
]
#field_type str(oci) str(citing) str(cited) datetime(creation) duration(timespan) str(?journal_sc) str(?author_sc)
#sparql PREFIX cito: <http://purl.org/spar/cito/>
SELECT ?id ?citing ?cited ?creation ?timespan (IF(bound(?jsc), "yes", "no") as ?journal_sc) (IF(bound(?asc), "yes", "no") as ?author_sc)
WHERE {
    GRAPH <https://w3id.org/oc/index/doci/> {
        BIND(<https://doi.org/[[doi]]> as ?citing_iri) .
        ?citing_iri ^cito:hasCitingEntity ?oci_iri .
        ?oci_iri
            cito:hasCitedEntity ?cited_iri ;
            cito:hasCitationCreationDate ?creation .

        OPTIONAL {
          ?oci_iri cito:hasCitationTimeSpan ?timespan .
        }

        OPTIONAL {
            ?oci_iri a cito:JournalSelfCitation .
            BIND("yes" as ?jsc)
        }

        OPTIONAL {
            ?oci_iri a cito:AuthorSelfCitation .
            BIND("yes" as ?asc)
        }

        BIND(STRAFTER(str(?citing_iri), "https://doi.org/") AS ?citing_doi)
        BIND(CONCAT("doi:", str(?citing_doi)) as ?citing)
        BIND(STRAFTER(str(?cited_iri), "https://doi.org/") AS ?cited_doi)
        BIND(CONCAT("doi:", str(?cited_doi)) as ?cited)
        BIND(STRAFTER(str(?oci_iri), "/ci/") AS ?id_val)
        BIND(CONCAT("oci:", str(?id_val)) as ?id)
    }
}
LIMIT 50000

#url /citations/{doi}
#type operation
#doi str(10\..+)
#preprocess lower(doi) --> encode(doi)
#postprocess decode_doi(citing, cited)
#method get
#description This operation retrieves the citation data for all the references appearing in the reference lists of other citing works to the bibliographic entity identified by the input DOI, that constitute the incoming citations of that identified bibliographic entity.

The fields returned by this operation are:

* *oci*: the Open Citation Identifier (OCI) of the citation in consideration;
* *citing*: the DOI of the citing entity;
* *cited*: the DOI of the cited entity;
* *creation*: the creation date of the citation according to the [ISO date format](https://en.wikipedia.org/wiki/ISO_8601) `YYYY-MM-DD`, which corresponds to the publication date of the citing entity;
* *timespan*: the interval between the publication date of the cited entity and the publication date of the citing entity, expressed using the [XSD duration format](https://www.w3.org/TR/xmlschema11-2/#duration) `PnYnMnD`;
* *journal_sc*: it records whether the citation is a journal self-citations (i.e. the citing and the cited entities are published in the same journal);
* *author_sc*: it records whether the citation is an author self-citation (i.e. the citing and the cited entities have at least one author in common).
#call /citations/10.5281/zenodo.3773001
#output_json [
    {
      "id": "oci:08005020801363514232413243703070907000600-08005020801363514232413243703070703000001",
      "citing": "doi:10.5281/zenodo.3797060",
      "cited": "doi:10.5281/zenodo.3773001",
      "creation": "2020-04-28",
      "timespan": "P0D",
      "journal_sc": "no",
      "author_sc": "no"
    },
    {
      "id": "oci:08005020801363514232413243703070907000604-08005020801363514232413243703070703000001",
      "citing": "doi:10.5281/zenodo.3797064",
      "cited": "doi:10.5281/zenodo.3773001",
      "creation": "2020-04-28",
      "timespan": "P0D",
      "journal_sc": "no",
      "author_sc": "no"
    },
    {
      "id": "oci:08005020801363514232413243703070907000606-08005020801363514232413243703070703000001",
      "citing": "doi:10.5281/zenodo.3797066",
      "cited": "doi:10.5281/zenodo.3773001",
      "creation": "2020-04-28",
      "timespan": "P0D",
      "journal_sc": "no",
      "author_sc": "no"
    }
]
#field_type str(oci) str(citing) str(cited) datetime(creation) duration(timespan) str(?journal_sc) str(?author_sc)
#sparql PREFIX cito: <http://purl.org/spar/cito/>
SELECT ?id ?citing ?cited ?creation ?timespan (IF(bound(?jsc), "yes", "no") as ?journal_sc) (IF(bound(?asc), "yes", "no") as ?author_sc)
WHERE {
    GRAPH <https://w3id.org/oc/index/doci/> {
        BIND(<https://doi.org/[[doi]]> as ?cited_iri) .
        ?cited_iri ^cito:hasCitedEntity ?oci_iri .
        ?oci_iri
            cito:hasCitingEntity ?citing_iri ;
            cito:hasCitationCreationDate ?creation .

        OPTIONAL {
            ?oci_iri cito:hasCitationTimeSpan ?timespan .
        }

        OPTIONAL {
            ?oci_iri a cito:JournalSelfCitation .
            BIND("yes" as ?jsc)
        }

        OPTIONAL {
            ?oci_iri a cito:AuthorSelfCitation .
            BIND("yes" as ?asc)
        }

        BIND(STRAFTER(str(?citing_iri), "https://doi.org/") AS ?citing_doi)
        BIND(CONCAT("doi:", str(?citing_doi)) as ?citing)
        BIND(STRAFTER(str(?cited_iri), "https://doi.org/") AS ?cited_doi)
        BIND(CONCAT("doi:", str(?cited_doi)) as ?cited)
        BIND(STRAFTER(str(?oci_iri), "/ci/") AS ?id_val)
        BIND(CONCAT("oci:", str(?id_val)) as ?id)
    }
}
LIMIT 50000

#url /citation/{oci}
#type operation
#oci str([0-9]+-[0-9]+)
#method get
#postprocess decode_doi(citing, cited)
#description This operation retrieves the citation metadata for the citation identified by the input Open Citation Identifier (OCI).

The Open Citation Identifier is a globally unique persistent identifier for bibliographic citations, which has a simple structure: the lower-case letters "oci" followed by a colon, followed by two numbers separated by a dash. For example, `oci:1-18` is a valid OCI.

It is worth mentioning that, in this REST operation, the prefix "oci:" **should not** be specified, and only the dash-separated numbers of the OCI should be provided, as shown in the example below.

The fields returned by this operation are:

* *oci*: the Open Citation Identifier (OCI) of the citation in consideration;
* *citing*: the DOI of the citing entity;
* *cited*: the DOI of the cited entity;
* *creation*: the creation date of the citation according to the [ISO date format](https://en.wikipedia.org/wiki/ISO_8601) `YYYY-MM-DD`, which corresponds to the publication date of the citing entity;
* *timespan*: the interval between the publication date of the cited entity and the publication date of the citing entity, expressed using the [XSD duration format](https://www.w3.org/TR/xmlschema11-2/#duration) `PnYnMnD`;
* *journal_sc*: it records whether the citation is a journal self-citations (i.e. the citing and the cited entities are published in the same journal);
* *author_sc*: it records whether the citation is an author self-citation (i.e. the citing and the cited entities have at least one author in common).
#call /citation/08005020801363514232413243703070907000604-08005020801363514232413243703070703000001
#output_json [
    {
      "id": "oci:08005020801363514232413243703070907000604-08005020801363514232413243703070703000001",
      "citing": "doi:10.5281/zenodo.3797064",
      "cited": "doi:10.5281/zenodo.3773001",
      "creation": "2020-04-28",
      "timespan": "P0D",
      "journal_sc": "no",
      "author_sc": "no"
    }
]
#field_type str(oci) str(citing) str(cited) datetime(creation) duration(timespan) str(?journal_sc) str(?author_sc)
#sparql PREFIX cito: <http://purl.org/spar/cito/>
SELECT ?id ?citing ?cited ?creation ?timespan (IF(bound(?jsc), "yes", "no") as ?journal_sc) (IF(bound(?asc), "yes", "no") as ?author_sc)
WHERE {
    GRAPH <https://w3id.org/oc/index/doci/> {
        BIND(<https://w3id.org/oc/index/doci/ci/[[oci]]> as ?oci_iri) .
        ?oci_iri
            cito:hasCitingEntity ?citing_iri ;
            cito:hasCitedEntity ?cited_iri ;
            cito:hasCitationCreationDate ?creation .

        OPTIONAL {
            ?oci_iri cito:hasCitationTimeSpan ?timespan .
        }

        OPTIONAL {
            ?oci_iri a cito:JournalSelfCitation .
            BIND("yes" as ?jsc)
        }

        OPTIONAL {
            ?oci_iri a cito:AuthorSelfCitation .
            BIND("yes" as ?asc)
        }

        BIND(STRAFTER(str(?citing_iri), "https://doi.org/") AS ?citing_doi)
        BIND(CONCAT("doi:", str(?citing_doi)) as ?citing)
        BIND(STRAFTER(str(?cited_iri), "https://doi.org/") AS ?cited_doi)
        BIND(CONCAT("doi:", str(?cited_doi)) as ?cited)
        BIND(STRAFTER(str(?oci_iri), "/ci/") AS ?id_val)
        BIND(CONCAT("oci:", str(?id_val)) as ?id)
    }
}
LIMIT 50000

#url /citation-count/{doi}
#type operation
#doi str(10\..+)
#preprocess lower(doi) --> encode(doi)
#method get
#description This operation retrieves the number of incoming citations to the bibliographic entity identified by the input DOI.

The field returned by this operation is:

* *count*: the number of incoming citations to the input bibliographic entity.
#call /citation-count/10.5281/zenodo.3773001
#field_type int(count)
#output_json [
    {
        "count": "3"
    }
]
#sparql PREFIX cito: <http://purl.org/spar/cito/>
SELECT (count(?c) as ?count)
WHERE {
    GRAPH <https://w3id.org/oc/index/doci/> {
        BIND(<https://doi.org/[[doi]]> as ?cited) .
		?cited ^cito:hasCitedEntity ?c
    }
}

#url /reference-count/{doi}
#type operation
#doi str(10\..+)
#preprocess lower(doi) --> encode(doi)
#method get
#description This operation retrieves the number of outgoing citations from the bibliographic entity identified by the input DOI.

The field returned by this operation is:

* *count*: the number of outgoing citations from the input bibliographic entity.
#call /reference-count/10.5281/zenodo.3797060
#field_type int(count)
#output_json [
    {
        "count": "10"
    }
]
#sparql PREFIX cito: <http://purl.org/spar/cito/>
SELECT (count(?c) as ?count)
WHERE {
    GRAPH <https://w3id.org/oc/index/doci/> {
        BIND(<https://doi.org/[[doi]]> as ?citing) .
		?citing ^cito:hasCitingEntity ?c
    }
}

#url /metadata/{dois}
#type operation
#dois str(\"?10\..+[^_\"]((__|\" \")10\..+[^_])*\"?)
#preprocess lower(dois) --> encode(dois) --> split_dois(dois)
#postprocess metadata(False, doi)
#method get
#description This operation retrieves the bibliographic metadata for each of the bibliographic entities identified by one or more input DOIs.

It is possible to specify one or more DOIs as input of this operation. In this case, the DOI should be separated with a double underscore ("\_\_") â€“ e.g. "10.1108/jd-12-2013-0166\_\_10.1016/j.websem.2012.08.001\_\_...". The fields returned by this operation are:

* *author*: the semicolon-separated list of authors of the bibliographic entity, together with their ids between square brackets;
* *pub_date*: the publication date of the bibliographic entity;
* *title*: the title of the bibliographic entity;
* *venue*: The venue where the bibliographic entity was published and its ids between square brackets separated by space;
* *volume*: the number of the volume in which the bibliographic entity was published;
* *issue*: the number of the issue in which the bibliographic entity was published;
* *page*: the starting and ending pages of the bibliographic entity in the context of the venue where it was published;
* *id*: the space separated ids of the bibliographic entity;
* *reference*: the semicolon-separated MetaIDs of all the entities cited by the bibliographic entity, according to the citation data available in all the OpenCitations Indexes;
* *citation*: the semicolon-separated MetaIDs of all the entities that cite the bibliographic entity, according to the citation data available in all the OpenCitations Indexes;
* *citation_count*: the number of citations received by the bibliographic entity;

#call /metadata/10.1108/jd-12-2013-0166__10.6084/m9.figshare.3443876
#field_type str(author) str(editor) datetime(pub_date) str(title) str(venue) str(volume) str(issue) str(page) str(id) str(reference) str(citation) int(citation_count)
#output_json [
    {
        "citation": "10.1177/0961000615616450; 10.7554/elife.32822; 10.1145/3197026.3197050; 10.1093/bib/bbx057; 10.1007/978-3-319-68204-4_19; 10.3346/jkms.2015.30.11.1545; 10.1007/978-3-319-73165-0_24; 10.3233/sw-160224; 10.3233/sw-180307; 10.1007/978-3-319-90548-8_7; 10.1142/s021964921850034x; 10.1007/978-3-319-58694-6_23; 10.1057/s41275-017-0070-x",
        "reference": "10.1001/jama.295.1.90; 10.1002/asi.4630240406; 10.1002/(sici)1097-4571(198909)40:5<342::aid-asi7>3.0.co;2-u; 10.1007/bf02457980; 10.1007/s10579-012-9211-2; 10.1007/s11192-009-0021-2; 10.1016/j.websem.2012.08.001; 10.1016/j.websem.2013.05.001; 10.1023/a:1021919228368; 10.1038/35079151; 10.1038/495437a; 10.1038/502295a; 10.1038/502298a; 10.1042/bj20091474; 10.1073/pnas.0407743101; 10.1087/2009202; 10.1093/bioinformatics; 10.1101/sqb.1972.036.01.015; 10.1108/eum0000000007123; 10.1108/jd-07-2012-0082; 10.1126/science.149.3683.510; 10.1136/bmj.a568; 10.1136/bmj.b2680; 10.1145/1498765.1498780; 10.1177/030631277400400102; 10.1177/030631277500500106; 10.1371/journal.pcbi.0010034; 10.1371/journal.pcbi.1000361; 10.1371/journal.pntd.0000228; 10.1371/journal.pone.0000308; 10.1523/jneurosci.0003-08.2008; 10.1525/bio.2010.60.5.2; 10.3115/1610075.1610091; 10.5210/fm.v2i4.522; 10.5539/ass.v9n5p18; 10.7717/peerj.175",
        "author": "Peroni, Silvio; Dutton, Alexander; Gray, Tanya; Shotton, David",
        "volume": "71",
        "page": "253-277",
        "citation_count": "13",
        "source_id": "issn:0022-0418",
        "source_title": "Journal Of Documentation",
        "year": "2015",
        "oa_link": "",
        "doi": "10.1108/jd-12-2013-0166",
        "title": "Setting Our Bibliographic References Free: Towards Open Citation Data",
        "issue": "2"
    },
    {
        "citation": "10.1007/978-3-319-68204-4_19",
        "reference": "",
        "author": "Peroni, Silvio, 0000-0003-0530-4305; Shotton, David, 0000-0001-5506-523X",
        "volume": "",
        "page": "",
        "citation_count": "1",
        "source_id": "",
        "source_title": "Figshare",
        "year": "2018",
        "oa_link": "",
        "doi": "10.6084/m9.figshare.3443876",
        "title": "The OpenCitations Data Model",
        "issue": ""
    }
]
#sparql PREFIX cito: <http://purl.org/spar/cito/>
SELECT DISTINCT ?id ?citation_count ?citation (GROUP_CONCAT(?cited_doi; separator=" ") as ?reference)
WITH {
      SELECT ?citing (count(?c) as ?c_count) (GROUP_CONCAT(?citing_source; separator=" ") as ?citation) {
        GRAPH <https://w3id.org/oc/index/doci/> {
          VALUES ?doi { [[dois]] }
          BIND(IRI(CONCAT("https://doi.org/", ?doi)) as ?citing)
    	    ?citing ^cito:hasCitedEntity ?c .
    	    ?c cito:hasCitingEntity ?source .
    	    BIND(CONCAT("doi:", STRAFTER(str(?source), "https://doi.org/")) as ?citing_source)
        }
      } GROUP BY ?citing
} AS %results
WHERE {
    GRAPH <https://w3id.org/oc/index/doci/> {
        OPTIONAL {
            INCLUDE %results
        }
        VALUES ?doi { [[dois]] }
        BIND(IRI(CONCAT("https://doi.org/", ?doi)) AS ?citing)
        OPTIONAL {
            ?citing ^cito:hasCitingEntity / cito:hasCitedEntity ?cited .
            BIND(CONCAT("doi:", STRAFTER(str(?cited), "https://doi.org/")) as ?cited_doi)
        }
        BIND(IF(bound(?c_count), ?c_count, 0) as ?citation_count)
        BIND(CONCAT("doi:", ?doi) AS ?id)
    }
} GROUP BY ?id ?citation_count ?citation
LIMIT 50000